plugins {
    id 'com.github.johnrengelman.shadow'
    id 'com.diffplug.spotless' version "7.0.0.BETA3"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
    implementation "mod.azure.azurelib:azurelib-neo-${minecraft_version}:${azurelib_version}"
    implementation "curse.maven:worldedit-225608:5830452"
    modApi "me.shedaniel.cloth:cloth-config-neoforge:${rootProject.cloth_config_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

spotless {
    java {
        eclipse().configFile("$rootDir/eclipse-formatter.xml")
        endWithNewline()
        importOrder("", "java", group.toString(), "\\#")
        indentWithSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
    }
}

tasks.build {
    dependsOn("spotlessApply")
}
